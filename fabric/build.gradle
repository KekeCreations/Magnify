plugins {
    id 'java'
    id 'idea'
    id 'maven-publish'
    id 'fabric-loom'
}
base {
    archivesName = "${mod_name}-fabric-${minecraft_version}"
}
repositories {
    maven {
        name = 'Ladysnake Mods'
        url = 'https://maven.ladysnake.org/releases'
    }
}
dependencies {
    minecraft "com.mojang:minecraft:${minecraft_version}"
    mappings loom.officialMojangMappings()
    modImplementation "net.fabricmc:fabric-loader:${fabric_loader_version}"
    modImplementation "net.fabricmc.fabric-api:fabric-api:${fabric_version}"
    implementation group: 'com.google.code.findbugs', name: 'jsr305', version: '3.0.1'
    implementation project(":common")

    //DEV QOL
    modImplementation "curse.maven:modmenu-308702:5162837"
    modImplementation "curse.maven:jei-238222:5604705"

    //COMPAT
    modImplementation "curse.maven:artsandcrafts-1034791:5579910"
    modImplementation "curse.maven:comforts-276951:5503514"
    modImplementation "dev.onyxstudios.cardinal-components-api:cardinal-components-base:5.2.1"
    modImplementation "dev.onyxstudios.cardinal-components-api:cardinal-components-entity:5.2.1"
    modImplementation "curse.maven:spectrelib-993948:5217601"
    modImplementation 'com.electronwill.night-config:toml:3.6.0'
    modImplementation "curse.maven:caffeinated-835826:4659359"
    modImplementation "curse.maven:snow-pig-fabric-469594:5396518"
    modImplementation "curse.maven:pigsteel-448941:5648715"
    modImplementation "curse.maven:trimmed-857036:4845942"
}

loom {
    if (project(":common").file("src/main/resources/${mod_id}.accesswidener").exists()) {
        accessWidenerPath.set(project(":common").file("src/main/resources/${mod_id}.accesswidener"))
    }
    runs {
        client {
            client()
            setConfigName("Fabric Client")
            ideConfigGenerated(true)
            runDir("run")
        }
        server {
            server()
            setConfigName("Fabric Server")
            ideConfigGenerated(true)
            runDir("run")
        }
    }
}

tasks.withType(JavaCompile).configureEach {
    source(project(":common").sourceSets.main.allSource)
}
tasks.withType(Javadoc).configureEach {
    source(project(":common").sourceSets.main.allJava)
}
tasks.named("sourcesJar", Jar) {
    from(project(":common").sourceSets.main.allSource)
}

processResources {
    from project(":common").sourceSets.main.resources
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            artifactId base.archivesName.get()
            from components.java
        }
    }
    repositories {
        maven {
            url "file://" + System.getenv("local_maven")
        }
    }
}
